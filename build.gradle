plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

subprojects { proj ->

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    description = 'Structurizr'
    group = 'com.structurizr'
    version = '1.0.0-RC4'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
            }
        }
        test {
            java {
                srcDir 'test/unit'
            }
        }
    }

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        manifest {
            attributes(
                    "Implementation-Title": "Structurizr for Java",
                    "Implementation-Version": version
            )
        }
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    def pomConfig = {
        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution "repo"
            }
        }
        developers {
            developer {
                id "simon"
                name "Simon Brown"
                email "simon@structurizr.com"
            }
        }

        scm {
            url "https://github.com/structurizr/java"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar
                artifact javadocJar
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'Visualise, document and explore your software architecture.')
                    root.appendNode('name', 'Structurizr for Java')
                    root.appendNode('url', 'https://github.com/structurizr/java')
                    root.children().last() + pomConfig
                }
            }
        }

        repositories {
            maven {
                url "$buildDir/repo"
            }
        }
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        publications = ['mavenJava']
        pkg {
            repo = 'maven'
            name = 'structurizr-java'
            userOrg = 'structurizr'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/structurizr/java.git'
            publicDownloadNumbers = true
            version {
                name = '1.0.0-RC4'
                desc = 'Structurizr for Java 1.0.0-RC4'
                released  = new Date()
                vcsTag = '1.0.0-RC4'
            }
        }
    }
}